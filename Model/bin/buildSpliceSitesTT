#!/usr/bin/perl

use strict;

use lib "$ENV{GUS_HOME}/lib/perl";

use DBI;
use DBD::Oracle;

use Getopt::Long qw(GetOptions);

use Bio::SeqFeature::Generic;
use Bio::SeqFeature::Collection;

use Data::Dumper;

use EbrcModelCommon::Model::tmUtils;

my ($help, $propfile, $instance, $schema, $suffix, $prefix, $filterValue, $debug);

Getopt::Long::Configure("pass_through");
GetOptions("propfile=s" => \$propfile,
           "instance=s" => \$instance,
           "schema=s" => \$schema,
           "suffix=s" => \$suffix,
           "prefix=s" => \$prefix,
           "filterValue=s" => \$filterValue,
           "debug!" => \$debug,
           "help|h" => \$help,
          );

die "required parameter missing" unless ($propfile && $instance && $suffix);

my $dbh = EbrcModelCommon::Model::tmUtils::getDbHandle($instance, $schema, $propfile);


&run();

#--------------------------------------------------------------------------------

sub run{
  &usage() if($help);

  &createEmptyTable($dbh,$suffix);

  # Coding end is always bigger than coding start
  my $transcriptLocSql = "select ta.gene_source_id
, ta.source_id
, ta.coding_start
, ta.coding_end
, ta.is_reversed
from transcriptattributes ta
, (select source_id from apidb.polyagenes
   union
   select source_id from apidb.splicesitegenes) ss
where ss.source_id = ta.gene_source_id
 and ta.coding_start is not null
-- TODO remove
--and ta.gene_source_id in ( 'Tb11.v5.0365')
";

  my $tsh = $dbh->prepare($transcriptLocSql);
  $tsh->execute();

  my %geneTranscriptLocations;
  while(my ($gene, $transcript, $cdsStart, $cdsEnd, $isReversed) = $tsh->fetchrow_array()) {
    push @{$geneTranscriptLocations{$gene}}, [$transcript, $cdsStart, $cdsEnd, $isReversed];
  }

  my $ssSql =  "select * from (
select ssg.source_id
  , ssf.segment_start
  , ssf.is_unique
  , ssg.dist_to_first_atg
  , sum(ssf.count_per_million) sum_cpm
  , ssf.type
  , ssf.na_sequence_id
  , ssf.strand
from apidb.splicesitegenes ssg
 , apidb.splicesitefeature ssf
where ssg.splice_site_feature_id = ssf.splice_site_feature_id
--TODO Remove
--and ssg.source_id in ( 'Tb11.v5.0365')
group by ssg.source_id, ssf.segment_start, ssf.is_unique, ssg.dist_to_first_atg, ssf.type, ssf.na_sequence_id, ssf.strand
union
select ssg.source_id
  , ssf.segment_start
  , ssf.is_unique
  , null
  , sum(ssf.count_per_million) sum_cpm
  , ssf.type
  , ssf.na_sequence_id
  , ssf.strand
from apidb.polyagenes ssg
 , apidb.splicesitefeature ssf
where ssg.splice_site_feature_id = ssf.splice_site_feature_id
--TODO Remove
--and ssg.source_id in ( 'Tb11.v5.0365')
group by ssg.source_id, ssf.segment_start, ssf.is_unique, ssf.type, ssf.na_sequence_id, ssf.strand
)  t
order by source_id";

  my $prevGene;
  my $sh = $dbh->prepare($ssSql);
  $sh->execute();

  my $insertSql = "INSERT INTO SpliceSiteTranscripts$suffix (location, type, na_sequence_id, is_unique, sum_cpm, dist_to_first_atg, transcript_source_id, dist_to_cds, is_dominant, gene_source_id, strand) VALUES (?,?,?,?,?,?,?,?,?,?,?)";
  my $insertSh = $dbh->prepare($insertSql);

  my (@splicedLeaderFeatures, @polyAFeatures, $minLoc, $maxLoc, $polyAStrand, $slStrand);
  while(my ($gene, $loc, $isUniq, $distToFirstAtg, $sumCpm, $type, $naSequenceId, $ssfStrand) = $sh->fetchrow_array()) {
    if($prevGene && $prevGene ne $gene) {

      &makePolyAAndSplicedLeaderRows($insertSh, $geneTranscriptLocations{$prevGene}, \@polyAFeatures, \@splicedLeaderFeatures, $minLoc, $maxLoc, $prevGene, $polyAStrand, $slStrand);

      @splicedLeaderFeatures = ();
      @polyAFeatures = ();
      $minLoc = undef;
      $maxLoc = undef;
      $polyAStrand = undef;
      $slStrand = undef;
    }

    $minLoc = $loc if(! defined($minLoc) || $loc < $minLoc);
    $maxLoc = $loc if(! defined($maxLoc) || $loc > $maxLoc);

    my $feature = Bio::SeqFeature::Generic->new(-start => $loc, 
                                                -end => $loc, 
                                                -primary_tag => $type, 
                                                -seq_id => $naSequenceId, 
                                                -tag => { 'is_unique' => $isUniq,  'sum_cpm' => $sumCpm, 'gene' => $gene, 'dist_to_first_atg' => $distToFirstAtg, 'strand' => $ssfStrand}
        );

    if($type eq 'Poly A') {
      push @polyAFeatures, $feature;
      $polyAStrand = $ssfStrand
    }
    else {
      push @splicedLeaderFeatures, $feature;
      $slStrand = $ssfStrand
    }


    $prevGene = $gene;
  }

  if($prevGene) {
    &makePolyAAndSplicedLeaderRows($insertSh, $geneTranscriptLocations{$prevGene}, \@polyAFeatures, \@splicedLeaderFeatures, $minLoc, $maxLoc, $prevGene, $polyAStrand, $slStrand);
  }

 # &createIndex($dbh,$suffix);

  $dbh->commit();
  $dbh->disconnect();

}

#--------------------------------------------------------------------------------

sub makePolyAAndSplicedLeaderRows {
  my ($insertSh, $transcriptLocations, $polyAFeatures, $splicedLeaderFeatures, $minLoc, $maxLoc, $gene, $polyAStrand, $slStrand) = @_;

  return unless($transcriptLocations);

  my @sortedTranscriptLocations = sort { $a->[1] <=> $b->[1] || $a->[2] <=> $b->[2] } @$transcriptLocations;

  my $splicedLeaderCollection = Bio::SeqFeature::Collection->new();
  $splicedLeaderCollection->add_features($splicedLeaderFeatures);
  &processSpliceSites($insertSh, \@sortedTranscriptLocations, $splicedLeaderCollection, $minLoc, $maxLoc, 'SL', $gene, $slStrand);

  my $polyACollection = Bio::SeqFeature::Collection->new();
  $polyACollection->add_features($polyAFeatures);
  &processSpliceSites($insertSh, \@sortedTranscriptLocations, $polyACollection, $minLoc, $maxLoc, 'PA', $gene, $polyAStrand);
}

#--------------------------------------------------------------------------------

sub processSpliceSites {
  my  ($insertSh, $transcriptLocations, $collection, $minLoc, $maxLoc, $typeAbbrev, $gene, $strand) = @_;

  my ($start, $end, @locations, $swap, $index);
  if(($typeAbbrev eq 'SL' && $strand eq '+') || ($typeAbbrev eq 'PA' && $strand eq '+')) {
    $start = $minLoc;
    $end = $maxLoc;
    $index = 1;
    @locations = @$transcriptLocations;
  }
  elsif(($typeAbbrev eq 'SL' && $strand eq '-') || ($typeAbbrev eq 'PA' && $strand eq '-')) {
    $start = $maxLoc;
    $end = $minLoc;
    $index = 2;
    $swap = 1;
    @locations = reverse @$transcriptLocations;
  }


  my $prev = $start;

  for(my $i = 0; $i < scalar @locations; $i++) {

    my $transcript = $locations[$i]->[0];
    my $value = $locations[$i]->[$index];

    &processRegion($insertSh, $collection, $prev, $value, $typeAbbrev, $swap, $transcript, $gene);

    my $pIndex = $i + 1;
    if($locations[$pIndex]) {
      if($locations[$pIndex]->[$index] == $value) { # if 2 transcripts have same loc then want to keep the prev loc
        print STDERR "Location [$value] for transcript $transcript is shared w/ another transcript.  Splice sites assigned will be the same for all\n" if($debug);
      }
      else {
        $prev = $swap ? $value - 1: $value + 1;
      }
    }
    else {
      $prev = $swap ? $value - 1: $value + 1;
    }
  }

  my $lengthLocations = scalar @locations;

  &processRegion($insertSh, $collection, $prev, $end, $typeAbbrev, $swap, undef, $gene);
}

#--------------------------------------------------------------------------------

sub processRegion {
  my ($insertSh, $collection, $start, $end, $typeAbbrev, $swap, $transcript, $gene) = @_;

  my $s = $swap ? $end : $start;
  my $e = $swap ? $start : $end;

  if($s >= $e) {
    print STDERR "Skipping segment from $s to $e for transcript [$transcript];  no splice sites in that region\n" if($debug);
    return;
  }
  my @subset = $collection->features_in_range(-start => $s,
                                              -end => $e,
                                              -contain => 1);

  my $dominantCpm = &getDominantCpm(\@subset);

  foreach my $feature (@subset) {
    &insertRow($insertSh, $feature, $transcript, $end, $dominantCpm, $gene);
  }

}

#--------------------------------------------------------------------------------

sub insertRow {
  my ($insertSh, $feature, $transcript, $end, $dominantCpm, $gene) = @_;

  my $location = $feature->start();
  my $type = $feature->primary_tag();
  my $naSequenceId = $feature->seq_id();
  my ($isUnique) = $feature->get_tag_values('is_unique');
  my ($sumCpm) = $feature->get_tag_values('sum_cpm');
  my ($distToFirstAtg) = $feature->get_tag_values('dist_to_first_atg');
  my ($strand) = $feature->get_tag_values('strand');

  my $distToCds = defined($transcript) ? abs ($location - $end) : undef;
  my $isDominant = $sumCpm == $dominantCpm ? 1 : 0;

  $insertSh->execute($location, $type, $naSequenceId, $isUnique, $sumCpm, $distToFirstAtg, $transcript, $distToCds, $isDominant, $gene, $strand);
}

#--------------------------------------------------------------------------------

sub getDominantCpm {
  my ($features) = @_;

  return unless(scalar @$features > 0);

  my %distinctCpms;
  my $rv;
  
  foreach my $feat (@$features) {
    my ($cpm) = $feat->get_tag_values('sum_cpm');
    $distinctCpms{$cpm}++;

    if($rv) {
      $rv = $cpm if($cpm > $rv);
    }
    else {
      $rv = $cpm
    }
  }

  if($distinctCpms{$rv} > 1) {
    return undef;
  }

  return $rv;
}

#--------------------------------------------------------------------------------

sub createEmptyTable {
     my ($dbh, $suffix) = @_;

    $dbh->do(<<SQL) or die "creating table";
     create table SpliceSiteTranscripts$suffix (
	     location               numeric(10),
       type                   varchar(20),
       na_sequence_id         numeric(10),
       is_unique              numeric(1),
       sum_cpm                float8,
       dist_to_first_atg      numeric(10),
       gene_source_id         varchar(80),
       transcript_source_id   varchar(80),
       dist_to_cds            numeric(10),
       is_dominant            numeric(1),
       strand                 char(1)
  )
SQL
$dbh->{PrintError} = 0;

}

#--------------------------------------------------------------------------------

sub createIndex {
     my ($dbh, $suffix) = @_;

    $dbh->do(<<SQL) or die "creating index";

SQL
$dbh->{PrintError} = 0;

}

#--------------------------------------------------------------------------------

sub usage {
  my $e = shift;
  if($e) {
    print STDERR $e . "\n";
  }
  print STDERR "usage:  buildGbrowseImageUrlTT.pl --databaseInstance <instance> --userName <userName> --password <password>";
  exit;
}

1;

