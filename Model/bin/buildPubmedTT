#!/usr/bin/perl

use strict;

use lib "$ENV{GUS_HOME}/lib/perl";

use DBI;
use Getopt::Long qw(GetOptions);
use Data::Dumper;
use EbrcModelCommon::Model::tmUtils;
use ApiCommonModel::Model::Pubmed;

my ($help, $propfile, $instance, $schema, $suffix, $debug);

Getopt::Long::Configure("pass_through");
GetOptions("propfile=s" => \$propfile,
           "instance=s" => \$instance,
           "schema=s" => \$schema,
           "suffix=s" => \$suffix,
           "debug!" => \$debug,
           "help|h" => \$help,
          );

usage() if ($help);

die "required parameter missing" unless ($propfile && $instance && $suffix);

my $dbh = EbrcModelCommon::Model::tmUtils::getDbHandle($instance, $schema, $propfile);

# create table
$dbh->do(<<SQL) or die "creating table";
   create table Pubmed$suffix (
      pubmed_id   number(10),
      title       varchar2(400),
      authors     clob,
      doi         varchar2(100)
   ) nologging
SQL

# try getting wanted references from an existing version of the table
$dbh->{PrintError} = 0;
$dbh->{RaiseError} = 0;
my $rtn = $dbh->do(<<SQL);
  insert into Pubmed$suffix (pubmed_id, title, authors, doi)
  with wanted_pmids
  as (select to_number(pubmed_id) as pubmed_id
      from (  -- PMIDs in current Pubmed table
              select to_char(pubmed_id) as pubmed_id
              from (select pubmed_id
                    from Pubmed
                    group by pubmed_id
                    having count(*) = 1) -- avoid duplicates
            intersect
              -- PMIDs in DbRef
              select regexp_replace(primary_identifier, '[^0-9]', '')
              from sres.DbRef
              where db_ref_id in (select db_ref_id from dots.DbRefNaFeature)
                and external_database_release_id
                    in (select edd.external_database_release_id
                        from ExternalDbDatasetPresenter edd
                          where lower(edd.dataset_presenter_display_name) like '%pubmed%')))
  select p.pubmed_id, p.title, p.authors, p.doi
  from Pubmed p, wanted_pmids wp
  where P.pubmed_id = wp.pubmed_id
SQL

$dbh->{PrintError} = 1;
$dbh->{RaiseError} = 1;

my $recycled = ($rtn eq "0E0" || $rtn eq "") ? 0 : $rtn;
print STDERR "Already have citations for $recycled PMIDs\n";

# prepare insert
my $insertStmt = $dbh->prepare(<<SQL);
   insert into Pubmed$suffix
   (pubmed_id, doi, title, authors)
   values (?, ?, ?, ?)
SQL

# get PubMed IDs
my $stmt = $dbh->prepare(<<SQL) or print STDERR $dbh->errstr;
     select pubmed_id
     from (select distinct regexp_replace(primary_identifier, '[^0-9]', '') as pubmed_id
           from sres.DbRef
           where db_ref_id in (select db_ref_id from dots.DbRefNaFeature)
             and external_database_release_id
                 in (select edd.external_database_release_id
                     from ExternalDbDatasetPresenter edd
                       where lower(edd.dataset_presenter_display_name) like '%pubmed%')
             and regexp_replace(primary_identifier, '[^0-9]', '') is not null)
     where pubmed_id not in (select to_char(pubmed_id) from Pubmed$suffix)
     order by pubmed_id
SQL

$stmt->execute();
my @pmids;
my $pmidCount = 0;
while (my ($pmid) = $stmt->fetchrow_array()) {
  push(@pmids, $pmid);
  $pmidCount++;
}
$stmt->finish();

print STDERR "Need citations for $pmidCount PMIDs\n";

# process PMIDs in chunks, so resulting XML is wieldy
my $CHUNKSIZE = 100;
my $insertCount;

while (@pmids) {
  my @chunkOfPmids;
  for (my $i=0; ($i < $CHUNKSIZE) && @pmids; $i++) {
    push(@chunkOfPmids, pop(@pmids));
  }

  # create Pubmed object (and query NCBI for data)
  print STDERR "getting PubMed data from NCBI\n"
    if $debug;
  my $pubmed = ApiCommonModel::Model::Pubmed->new(\@chunkOfPmids);

  print STDERR "processing PubMed data\n"
    if $debug;
  # iterate through list, match up PubMed data, and insert
  foreach my $pmid (@chunkOfPmids) {
    my $doi = $pubmed->getDoi($pmid);
    my $title = $pubmed->getTitle($pmid);
    my $authors = $pubmed->getAuthors($pmid);

    $insertStmt->execute($pmid, $doi, $title, $authors)
      or die "inserting record for PubMed ID \"$pmid\"";
    $insertCount++;
    print STDERR "Inserted $insertCount PubMed records\n"
      unless ($insertCount % 1000);
  }
}

print STDERR "Inserted $insertCount PubMed records\n"
  if $insertCount;

# create index
$dbh->do(<<SQL) or die "creating index";
   create index pmix$suffix
      on Pubmed$suffix (pubmed_id, title, doi)
SQL

$dbh->disconnect();

sub usage {
  my $e = shift;
  if($e) {
    print STDERR $e . "\n";
  }
  print STDERR "
Build a table of PubMed records from PubMed IDs linked in the database.

usage:  buildPubmedTT -propfile prop_file -instance instance -schema schema -suffix suffix  [-debug] [-help]
";
  exit;
}

1;

